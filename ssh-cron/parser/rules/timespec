nr:
    NR
    {
        $$ = stol(d_scanner.matched());
    }
;

optStep:
    // empty
    {
        $$ = 1;
    }
|
    '/' nr
    {
        $$ = $2;
    }
;

addNr:
    nr
    {
        d_cronData.addNr($1);
    }
;

numberUnit:
    nr '-' nr optStep
    {
        d_cronData.addRange($1, $3, $4);
    }
|
    addNr
;

numberSequence:
    numberSequence ',' numberUnit
|
    numberUnit
;

timeEnds:
    WS
    {
        emsg.on();
    }
;

allSequence:
    '*'  optStep timeEnds   // empty set indicates `all valid specifications
    {
        d_cronData.setAll($2);
    }
;

numberSpec:
    allSequence                                     
|
    numberSequence timeEnds
;


timeUnit:
    numberUnit
|
    ID
    {
        d_cronData.addName(d_scanner.matched());
    }
;

timeSequence:
    timeSequence ',' timeUnit
|
    timeUnit
;


timeSpec:
    allSequence
|
    timeSequence timeEnds
;


